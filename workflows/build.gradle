apply plugin: 'net.corda.plugins.cordapp'
apply plugin: 'net.corda.plugins.quasar-utils'

cordapp {
    targetPlatformVersion corda_platform_version
    minimumPlatformVersion corda_platform_version
    workflow {
        name "Template Flows"
        vendor "AFBlockchain"
        licence "Apache License, Version 2.0"
        versionId 1
    }
}

sourceSets {
    main {
        resources {
            srcDir rootProject.file("config/dev")
        }
    }
    test {
        resources {
            srcDir rootProject.file("config/test")
        }
    }
    integrationTest {
        kotlin {
            compileClasspath += main.output + test.output
            runtimeClasspath += main.output + test.output
            srcDir file('src/integrationTest/kotlin')
        }
    }
}

configurations {
    integrationTestCompile.extendsFrom testCompile
    integrationTestRuntime.extendsFrom testRuntime
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"

    // Corda dependencies.
    cordaCompile "$corda_core_release_group:corda-core:$corda_core_release_version"
    cordaRuntime "$corda_release_group:corda:$corda_release_version"
    testCompile "$corda_release_group:corda-node-driver:$corda_release_version"

    // CorDapp dependencies.
    cordapp project(":contracts")
    cordapp "$accounts_release_group:accounts-contracts:$accounts_release_version"
    cordapp "$confidential_id_release_group:ci-workflows:$confidential_id_release_version"
    cordapp "$accounts_release_group:accounts-workflows:$accounts_release_version"

    // Testing
    testCompile fileTree("../libs") { include '*jar' }
    testCompile 'org.junit.jupiter:junit-jupiter:5.8.2'
    testCompile 'org.testcontainers:testcontainers:1.16.1'
    testCompile "org.testcontainers:junit-jupiter:1.16.1"
    testCompile 'com.google.guava:guava:31.0.1-jre'
    testCompile 'com.github.manosbatsis.corda.testacles:corda-testacles-nodedriver:0.24'
}

task integrationTest(type: Test, dependsOn: [":bundleCordapps"]) {
    useJUnitPlatform()
    testLogging.showStandardStreams = true
    testClassesDirs = sourceSets.integrationTest.output.classesDirs
    classpath = sourceSets.integrationTest.runtimeClasspath
}